; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "Launcher4Kodi"
#define MyAppVersion "1.0"
#define MyAppPublisher "baijuxavior@gmail.com"
#define MyAppExeName "Launcher4Kodi.exe"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{56C60B55-3F11-456C-A6EA-B401F7CFA763}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
VersionInfoProductVersion=1.0
VersionInfoVersion=1.0
AppPublisher={#MyAppPublisher}
AppMutex=launcher4kodi_mutex
DefaultDirName={pf32}\Launcher4Kodi
DefaultGroupName=Launcher4Kodi
OutputBaseFilename=Launcher4Kodi V1.0
Compression=lzma
SolidCompression=yes
OutputDir=.
WizardImageBackColor=clRed
DisableProgramGroupPage=true
Uninstallable=yes
UninstallFilesDir={app}
WizardImageFile=.\Installer Script Icons\WizModernImage-IS.bmp
WizardSmallImageFile=.\Installer Script Icons\WizModernSmallImage-IS.bmp
SetupIconFile=.\Installer Script Icons\Setup Icon.ico
ChangesAssociations=yes
CloseApplications=yes
[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Code] 

 // ------------------------------------------------------------- Look for already installed version  ---------------------------------------------------

function GetVersionInstalled( AppID: String ): String;   
var
   sPrevVersion: String;
begin
 
   if not RegQueryStringValue(HKLM,'Software\Microsoft\Windows\CurrentVersion\Uninstall\'+AppID+'_is1','DisplayVersion', sPrevVersion) then   //if not found in HKLM
   RegQueryStringValue(HKCU,'Software\Microsoft\Windows\CurrentVersion\Uninstall\'+AppID+'_is1' , 'DisplayVersion', sPrevVersion);        // look in HKCU
   Result := sPrevVersion;
end;


function GetUninstallString( AppID: String ): String; //Get uninstall.exe path of previous installation
var
   sUnInstallString: String;
begin
 
   if not RegQueryStringValue(HKLM,'Software\Microsoft\Windows\CurrentVersion\Uninstall\'+AppID+'_is1','UninstallString', sUnInstallString) then
   RegQueryStringValue(HKCU,'Software\Microsoft\Windows\CurrentVersion\Uninstall\'+AppID+'_is1' , 'UninstallString', sUnInstallString);
   Result := sUnInstallString;
end;

 function UnInstallOldVersion(): Integer;   
var
  sUnInstallString: String;
  iResultCode: Integer;
  sAppID: String;
begin
// Return Values:
// 1 - uninstall string is empty
// 2 - error executing the UnInstallString
// 3 - successfully executed the UnInstallString

  // default return value
  Result := 0;

  // get the uninstall string of the old app
   sAppID := '{56C60B55-3F11-456C-A6EA-B401F7CFA763}';
  sUnInstallString := GetUninstallString(sAppID);
  if sUnInstallString <> '' then begin
    sUnInstallString := RemoveQuotes(sUnInstallString);
    if Exec(sUnInstallString,'/SILENT' ,'', SW_SHOW, ewWaitUntilTerminated, iResultCode) then
      Result := 3
    else
      Result := 2;
  end else
    Result := 1;
end;

function UnInstallKodiLauncher(): Integer;   
var
  sUnInstallString: String;
  iResultCode: Integer;
  sAppID: String;
begin
// Return Values:
// 1 - uninstall string is empty
// 2 - error executing the UnInstallString
// 3 - successfully executed the UnInstallString

  // default return value
  Result := 0;

  // get the uninstall string of the old app
   sAppID := '{F4A80E18-9F0A-4743-8253-D4E5135E75BD}';
  sUnInstallString := GetUninstallString(sAppID);
  if sUnInstallString <> '' then begin
    sUnInstallString := RemoveQuotes(sUnInstallString);
    if Exec(sUnInstallString,'/SILENT' ,'', SW_SHOW, ewWaitUntilTerminated, iResultCode) then
      Result := 3
    else
      Result := 2;
  end else
    Result := 1;
end;

function InitializeSetup(): Boolean;
var
 sAppID: String;
 sPrevVersion: String;
 sAppIDKodiLauncher: String;
 sPrevVersionKodiLauncher: String;
 begin
 
  sAppID := '{56C60B55-3F11-456C-A6EA-B401F7CFA763}';
  sPrevVersion := GetVersionInstalled( sAppID );
  sAppIDKodiLauncher := '{F4A80E18-9F0A-4743-8253-D4E5135E75BD}';
  sPrevVersionKodiLauncher := GetVersionInstalled( sAppIDKodiLauncher );

  if ( sPrevVersion = '1.0' ) then
    if MsgBox ('Launcher4Kodi V1.0 is already installed. Do you want to install it again?', mbInformation, MB_yesno) = idyes then
        Result:= true

  if ( sPrevVersion > '1.0' ) then
        if MsgBox ('A higher version Launcher4Kodi V' + sPrevVersion + ' is already installed. Press Yes to uninstall it or No to exit the setup.', mbInformation, MB_yesno) = idno then
        Result:= false
       else
          begin 
            
            if UnInstallOldVersion() = 3 then
              Result:= true
          end;

  if ( sPrevVersionKodiLauncher = '1.0.0.0' ) then
        if MsgBox ('KodiLauncher' + sPrevVersion + ' detected. It will be uninstalled and Launcher4Kodi will be installed.', mbInformation, MB_OK) = idno then
        Result:= false
       else
          begin 
            
            if UnInstallKodiLauncher() = 3 then
              Result:= true
          end;

  if ( sPrevVersion = '' ) then
    Result := true;
 end;


 // ------------------------------------------------------------- Take mce remote regisrty backup before installation  ---------------------------------------------------

procedure CurStepChanged(CurStep: TSetupStep);   

var
  ReportMappingTableKey: string;
  ReportMappingTableKey_Backup: string;
begin
if (CurStep = ssInstall) and IsComponentSelected('OptionMCERegistry') then
  begin
    if RegQueryBinaryValue(HKEY_LOCAL_MACHINE, 'SYSTEM\CurrentControlSet\services\HidIr\Remotes\745a17a0-74d3-11d0-b6fe-00a0c90f57da', 'ReportMappingTable', ReportMappingTableKey) then     {if ReportMappingTable exists   }
       if RegQueryBinaryValue(HKEY_LOCAL_MACHINE, 'SYSTEM\CurrentControlSet\services\HidIr\Remotes\745a17a0-74d3-11d0-b6fe-00a0c90f57da', 'ReportMappingTable_Backup', ReportMappingTableKey_Backup) = false then    {if ReportMappingTable_Backup not exists   }
          RegWriteBinaryValue(HKEY_LOCAL_MACHINE, 'SYSTEM\CurrentControlSet\services\HidIr\Remotes\745a17a0-74d3-11d0-b6fe-00a0c90f57da', 'ReportMappingTable_Backup', ReportMappingTableKey); {Take backup}
          
 end;
 end;



 // ------------------------------------------------------------- Restore changed files and mce remote registry on uninstall ---------------------------------------------------

procedure CurUninstallStepChanged(CurUninstallStep: TUninstallStep);
var
  OldFile: string;
  BackupKey: string;

begin
  case CurUninstallStep of    
    usPostUninstall:
      begin
      RegWriteStringValue(HKEY_CURRENT_USER, 'Software\Microsoft\Windows NT\CurrentVersion\Winlogon', 'Shell', 'Explorer.exe');

      if RegQueryBinaryValue(HKEY_LOCAL_MACHINE, 'SYSTEM\CurrentControlSet\services\HidIr\Remotes\745a17a0-74d3-11d0-b6fe-00a0c90f57da', 'ReportMappingTable_Backup', BackupKey) then
       begin
        RegWriteBinaryValue(HKEY_LOCAL_MACHINE, 'SYSTEM\CurrentControlSet\services\HidIr\Remotes\745a17a0-74d3-11d0-b6fe-00a0c90f57da', 'ReportMappingTable', BackupKey);
        RegDeleteValue(HKEY_LOCAL_MACHINE, 'SYSTEM\CurrentControlSet\services\HidIr\Remotes\745a17a0-74d3-11d0-b6fe-00a0c90f57da', 'ReportMappingTable_Backup')
       end;

      OldFile := ExpandConstant('{userappdata}') + '\Kodi\userdata\advancedsettings.xml.backup'
      if FileExists(OldFile) then RenameFile(OldFile, ExpandConstant('{userappdata}') + '\Kodi\userdata\advancedsettings.xml') ;

      OldFile := ExpandConstant('{userappdata}') + '\Kodi\userdata\keymaps\keyboard.xml.backup'
      if FileExists(OldFile) then RenameFile(OldFile, ExpandConstant('{userappdata}') + '\Kodi\userdata\keymaps\keyboard.xml');
      
      OldFile := ExpandConstant('{pf32}') + '\Kodi\addons\skin.confluence\720p\DialogButtonMenu.xml.backup'
      if FileExists(OldFile) then RenameFile(OldFile, ExpandConstant('{pf32}') + '\Kodi\addons\skin.confluence\720p\DialogButtonMenu.xml');

      end;
end;
end;
 
 
[Components]
Name: "OptionMainApps"; Description: "Launcher4Kodi";   Types: full compact custom ; Flags: fixed
Name: "OptionShell"; Description: "Set Launcher4Kodi as Windows Shell";   Types: custom 
Name: "OptionShutdownMenu"; Description: "Custom Shutdown Menu (For Confluence Skin only)";   Types: custom
Name: "OptionCustomKeymap"; Description: "Custom Keymap File";  Types: custom
Name: "OptionMCERegistry"; Description: "MCE Remote Control Registry keys";  Types: custom
Name: "Option\OriginalMCE"; Description: "Original MCE Remote Control Registry keys";   Types: custom; Flags: exclusive
Name: "Option\CustomMCE"; Description: "Custom MCE Remote Control Registry keys";   Types: custom ; Flags: exclusive
Name: "OptionMetroStart"; Description: "Add Power Button icons to Metro Start";   Types: custom  ; MinVersion: 6.2
Name: "OptionDesktopPowerButtons"; Description: "Add Power Button icons to Desktop";   Types: custom  ; MinVersion: 6.2
Name: "OptionPowerContextMenu"; Description: "Add Power Options to Desktop Context Menu";   Types: custom  ; MinVersion: 6.1

[Dirs]
Name: {userappdata}\Launcher4Kodi

[Files]
Source: "{pf32}\Kodi\addons\skin.confluence\720p\DialogButtonMenu.xml"; DestDir: "{pf32}\Kodi\addons\skin.confluence\720p"; DestName: "DialogButtonMenu.xml.backup"; Flags: external skipifsourcedoesntexist onlyifdoesntexist uninsneveruninstall; Components: OptionShutdownMenu
Source: "{userappdata}\Kodi\userdata\keymaps\keyboard.xml"; DestDir: "{userappdata}\Kodi\userdata\keymaps"; DestName: "keyboard.xml.backup"; Flags: external skipifsourcedoesntexist onlyifdoesntexist uninsneveruninstall; Components: OptionCustomKeymap

Source: "Script Files\Launcher4Kodi.exe"; DestDir: {app}; Flags: ignoreversion     ; Components: OptionMainApps
Source: "Script Files\Launcher4Kodi.ico"; DestDir: {app}; Flags: ignoreversion     ; Components: OptionMainApps

Source: "XBMCLauncherGUI\XBMCLauncherGUI\bin\Release\Launcher4KodiGUI.exe"; DestDir: {app}; Flags: ignoreversion     ; Components: OptionMainApps

;Source: "Script Files\advancedsettings.xml"; DestDir: "{userappdata}\Kodi\userdata"; Flags: ignoreversion; Components: OptionAdvancedSettings

Source: "Script Files\keyboard.xml"; DestDir: "{userappdata}\Kodi\userdata\keymaps"; Flags: ignoreversion ; Components: OptionCustomKeymap

Source: "Script Files\ConfluenceShutdownButtonAction.exe"; DestDir: "{pf32}\Kodi"; Flags: ignoreversion   ; Components: OptionShutdownMenu
Source: "Script Files\ConfluenceShutdownButtonAction.py"; DestDir: "{pf32}\Kodi"; Flags: ignoreversion   ; Components: OptionShutdownMenu
Source: "Script Files\CloseKodi.exe"; DestDir: "{pf32}\Kodi"; Flags: ignoreversion   ; Components: OptionShutdownMenu
Source: "Script Files\CloseKodi.py"; DestDir: "{pf32}\Kodi"; Flags: ignoreversion   ; Components: OptionShutdownMenu
Source: "Script Files\RestartKodi.exe"; DestDir: "{pf32}\Kodi"; Flags: ignoreversion   ; Components: OptionShutdownMenu
Source: "Script Files\RestartKodi.py"; DestDir: "{pf32}\Kodi"; Flags: ignoreversion   ; Components: OptionShutdownMenu
Source: "Script Files\DialogButtonMenu32.xml"; DestDir: "{pf32}\Kodi\addons\skin.confluence\720p"; DestName: DialogButtonMenu.xml; Flags: ignoreversion ; Components: OptionShutdownMenu; Check: "not IsWin64"
Source: "Script Files\DialogButtonMenu64.xml"; DestDir: "{pf32}\Kodi\addons\skin.confluence\720p"; DestName: DialogButtonMenu.xml; Flags: ignoreversion ; Components: OptionShutdownMenu; Check: IsWin64

Source: "Script Files\ConfluenceShutdownButtonAction.exe"; DestDir: {app}; Flags: ignoreversion   ; Components: OptionShutdownMenu
Source: "Script Files\ConfluenceShutdownButtonAction.py"; DestDir: {app}; Flags: ignoreversion   ; Components: OptionShutdownMenu
Source: "Script Files\CloseKodi.exe"; DestDir: {app}; Flags: ignoreversion   ; Components: OptionShutdownMenu
Source: "Script Files\CloseKodi.py"; DestDir: {app}; Flags: ignoreversion   ; Components: OptionShutdownMenu
Source: "Script Files\RestartKodi.exe"; DestDir: {app}; Flags: ignoreversion   ; Components: OptionShutdownMenu
Source: "Script Files\RestartKodi.py"; DestDir: {app}; Flags: ignoreversion   ; Components: OptionShutdownMenu
 
Source: "Script Files\Windows8-HybridShutdown.exe"; DestDir: {app}; Flags: ignoreversion ;  Components: OptionMetroStart  OptionDesktopPowerButtons
Source: "Script Files\Windows8-Restart.exe"; DestDir: {app}; Flags: ignoreversion ;  Components: OptionMetroStart  OptionDesktopPowerButtons
Source: "Script Files\Windows8-Sleep.exe"; DestDir: {app}; Flags: ignoreversion ;  Components: OptionMetroStart  OptionDesktopPowerButtons

Source: "Script Files\Windows8-HybridShutdown.ico"; DestDir: {app}; Flags: ignoreversion ;  Components: OptionMetroStart  OptionDesktopPowerButtons
Source: "Script Files\Windows8-Restart.ico"; DestDir: {app}; Flags: ignoreversion ;  Components: OptionMetroStart  OptionDesktopPowerButtons
Source: "Script Files\Windows8-Sleep.ico"; DestDir: {app}; Flags: ignoreversion ;  Components: OptionMetroStart    OptionDesktopPowerButtons
    
Source: "Script Files\CustomMCERegistry.reg"; DestDir: {userappdata}\Launcher4Kodi; Flags: ignoreversion ; Components: OptionMCERegistry
Source: "Script Files\OriginalMCERegistry.reg"; DestDir: {userappdata}\Launcher4Kodi; Flags: ignoreversion ; Components: OptionMCERegistry
Source: "Script Files\PinToTaskbar.exe"; DestDir: {app}\; Flags: ignoreversion ; MinVersion: 6.1

[Registry]
Root: HKCU; Subkey: Software\Microsoft\Windows NT\CurrentVersion\Winlogon; ValueType: string; ValueName: Shell; ValueData: {app}\Launcher4Kodi.exe; Flags: noerror; Components: OptionShell
Root: HKCU; Subkey: Software\Launcher4Kodi; Flags: uninsdeletekey;
Root: HKCU; Subkey: Software\Launcher4Kodi; ValueType: string; ValueName: Kodi_Path; ValueData: {pf32}\Kodi\Kodi.exe; Flags: noerror createvalueifdoesntexist;
Root: HKCU; Subkey: Software\Launcher4Kodi; ValueType: string; ValueName: iMON_Path; ValueData: {pf32}\SoundGraph\iMON\iMON.exe; Flags: noerror createvalueifdoesntexist;

Root: HKCU; Subkey: Software\Launcher4Kodi; ValueType: string; ValueName: ShowCustomShutdownMenu; ValueData: 1; Flags: noerror; Components: OptionShutdownMenu
Root: HKCU; Subkey: Software\Launcher4Kodi; ValueType: string; ValueName: ShutdownAction; ValueData: u; Flags: noerror; Components: OptionShutdownMenu

Root: HKCR; Subkey: DesktopBackground\Shell\Power; ValueType: string; ValueName: MUIVerb; ValueData: Power; Flags: noerror uninsdeletekey; Components: OptionPowerContextMenu
Root: HKCR; Subkey: DesktopBackground\Shell\Power; ValueType: string; ValueName: SubCommands; ValueData: "Lock;SwitchUser;LogOff;Sleep;Hibernate;HybridShutdown;ShutDown;Restart"; Flags: noerror uninsdeletekey; Components: OptionPowerContextMenu; MinVersion: 6.2
Root: HKCR; Subkey: DesktopBackground\Shell\Power; ValueType: string; ValueName: SubCommands; ValueData: "Lock;SwitchUser;LogOff;Sleep;Hibernate;ShutDown;Restart"; Flags: noerror uninsdeletekey; Components: OptionPowerContextMenu; OnlyBelowVersion: 6.2
Root: HKCR; Subkey: DesktopBackground\Shell\Power; ValueType: string; ValueName: icon; ValueData: shell32.dll,-221; Flags: noerror uninsdeletekey; Components: OptionPowerContextMenu
Root: HKCR; Subkey: DesktopBackground\Shell\Power; ValueType: string; ValueName: Position; ValueData: bottom; Flags: noerror uninsdeletekey; Components: OptionPowerContextMenu

Root: HKLM; Subkey: SOFTWARE\Microsoft\Windows\CurrentVersion\Explorer\CommandStore\shell\Lock; ValueType: string; ValueName: ; ValueData: Lock; Flags: noerror uninsdeletekey; Components: OptionPowerContextMenu
Root: HKLM; Subkey: SOFTWARE\Microsoft\Windows\CurrentVersion\Explorer\CommandStore\shell\Lock; ValueType: string; ValueName: icon; ValueData: shell32.dll,-48; Flags: noerror uninsdeletekey; Components: OptionPowerContextMenu
Root: HKLM; Subkey: SOFTWARE\Microsoft\Windows\CurrentVersion\Explorer\CommandStore\shell\Lock\command; ValueType: string; ValueName: ; ValueData: "rundll32.exe user32.dll, LockWorkStation"; Flags: noerror uninsdeletekey; Components: OptionPowerContextMenu

Root: HKLM; Subkey: SOFTWARE\Microsoft\Windows\CurrentVersion\Explorer\CommandStore\shell\SwitchUser; ValueType: string; ValueName: ; ValueData: Switch User; Flags: noerror uninsdeletekey; Components: OptionPowerContextMenu
Root: HKLM; Subkey: SOFTWARE\Microsoft\Windows\CurrentVersion\Explorer\CommandStore\shell\SwitchUser; ValueType: string; ValueName: icon; ValueData: imageres.dll,-88; Flags: noerror uninsdeletekey; Components: OptionPowerContextMenu
Root: HKLM; Subkey: SOFTWARE\Microsoft\Windows\CurrentVersion\Explorer\CommandStore\shell\SwitchUser\command; ValueType: string; ValueName: ; ValueData: tsdiscon.exe; Flags: noerror uninsdeletekey; Components: OptionPowerContextMenu

Root: HKLM; Subkey: SOFTWARE\Microsoft\Windows\CurrentVersion\Explorer\CommandStore\shell\LogOff; ValueType: string; ValueName: ; ValueData: Log Off; Flags: noerror uninsdeletekey; Components: OptionPowerContextMenu
Root: HKLM; Subkey: SOFTWARE\Microsoft\Windows\CurrentVersion\Explorer\CommandStore\shell\LogOff; ValueType: string; ValueName: icon; ValueData: shell32.dll,-45; Flags: noerror uninsdeletekey; Components: OptionPowerContextMenu
Root: HKLM; Subkey: SOFTWARE\Microsoft\Windows\CurrentVersion\Explorer\CommandStore\shell\LogOff\command; ValueType: string; ValueName: ; ValueData: shutdown.exe -l; Flags: noerror uninsdeletekey; Components: OptionPowerContextMenu

Root: HKLM; Subkey: SOFTWARE\Microsoft\Windows\CurrentVersion\Explorer\CommandStore\shell\ShutDown; ValueType: string; ValueName: ; ValueData: Shutdown; Flags: noerror uninsdeletekey; Components: OptionPowerContextMenu ; OnlyBelowVersion: 6.2
Root: HKLM; Subkey: SOFTWARE\Microsoft\Windows\CurrentVersion\Explorer\CommandStore\shell\ShutDown; ValueType: string; ValueName: ; ValueData: Full Shutdown; Flags: noerror uninsdeletekey; Components: OptionPowerContextMenu ; MinVersion: 6.2
Root: HKLM; Subkey: SOFTWARE\Microsoft\Windows\CurrentVersion\Explorer\CommandStore\shell\ShutDown; ValueType: string; ValueName: icon; ValueData: shell32.dll,-28; Flags: noerror uninsdeletekey; Components: OptionPowerContextMenu
Root: HKLM; Subkey: SOFTWARE\Microsoft\Windows\CurrentVersion\Explorer\CommandStore\shell\ShutDown\command; ValueType: string; ValueName: ; ValueData: shutdown.exe -s -t 00; Flags: noerror uninsdeletekey; Components: OptionPowerContextMenu

Root: HKLM; Subkey: SOFTWARE\Microsoft\Windows\CurrentVersion\Explorer\CommandStore\shell\HybridShutdown; ValueType: string; ValueName: ; ValueData: Hybrid Shutdown; Flags: noerror uninsdeletekey; Components: OptionPowerContextMenu ; MinVersion: 6.2
Root: HKLM; Subkey: SOFTWARE\Microsoft\Windows\CurrentVersion\Explorer\CommandStore\shell\HybridShutdown; ValueType: string; ValueName: icon; ValueData: shell32.dll,-28; Flags: noerror uninsdeletekey; Components: OptionPowerContextMenu  ; MinVersion: 6.2
Root: HKLM; Subkey: SOFTWARE\Microsoft\Windows\CurrentVersion\Explorer\CommandStore\shell\HybridShutdown\command; ValueType: string; ValueName: ; ValueData: Shutdown -s -hybrid -t 00; Flags: noerror uninsdeletekey; Components: OptionPowerContextMenu   ; MinVersion: 6.2

Root: HKLM; Subkey: SOFTWARE\Microsoft\Windows\CurrentVersion\Explorer\CommandStore\shell\Restart; ValueType: string; ValueName: ; ValueData: Restart; Flags: noerror uninsdeletekey; Components: OptionPowerContextMenu
Root: HKLM; Subkey: SOFTWARE\Microsoft\Windows\CurrentVersion\Explorer\CommandStore\shell\Restart; ValueType: string; ValueName: icon; ValueData: shell32.dll,-290; Flags: noerror uninsdeletekey; Components: OptionPowerContextMenu
Root: HKLM; Subkey: SOFTWARE\Microsoft\Windows\CurrentVersion\Explorer\CommandStore\shell\Restart\command; ValueType: string; ValueName: ; ValueData: shutdown.exe -r -t 00; Flags: noerror uninsdeletekey; Components: OptionPowerContextMenu

Root: HKLM; Subkey: SOFTWARE\Microsoft\Windows\CurrentVersion\Explorer\CommandStore\shell\Sleep; ValueType: string; ValueName: ; ValueData: Sleep; Flags: noerror uninsdeletekey; Components: OptionPowerContextMenu
Root: HKLM; Subkey: SOFTWARE\Microsoft\Windows\CurrentVersion\Explorer\CommandStore\shell\Sleep; ValueType: string; ValueName: icon; ValueData: powercpl.dll,-514; Flags: noerror uninsdeletekey; Components: OptionPowerContextMenu
Root: HKLM; Subkey: SOFTWARE\Microsoft\Windows\CurrentVersion\Explorer\CommandStore\shell\Sleep\command; ValueType: string; ValueName: ; ValueData: "rundll32.exe powrprof.dll,SetSuspendState Sleep"; Flags: noerror uninsdeletekey; Components: OptionPowerContextMenu

Root: HKLM; Subkey: SOFTWARE\Microsoft\Windows\CurrentVersion\Explorer\CommandStore\shell\Hibernate; ValueType: string; ValueName: ; ValueData: Hibernate; Flags: noerror uninsdeletekey; Components: OptionPowerContextMenu
Root: HKLM; Subkey: SOFTWARE\Microsoft\Windows\CurrentVersion\Explorer\CommandStore\shell\Hibernate; ValueType: string; ValueName: icon; ValueData: imageres.dll,-101; Flags: noerror uninsdeletekey; Components: OptionPowerContextMenu
Root: HKLM; Subkey: SOFTWARE\Microsoft\Windows\CurrentVersion\Explorer\CommandStore\shell\Hibernate\command; ValueType: string; ValueName: ; ValueData: Shutdown.exe -h; Flags: noerror uninsdeletekey; Components: OptionPowerContextMenu


Root: HKLM; Subkey: SOFTWARE\Microsoft\Windows\CurrentVersion\Explorer\CommandStore\shell\Hibernate\command; ValueType: string; ValueName: ; ValueData: Shutdown.exe -h; Flags: noerror uninsdeletekey; Components: OptionPowerContextMenu


[Icons] 
Name: "{userstartup}\Launcher4Kodi"; Filename: {app}\Launcher4Kodi.exe
Name: {group}\Launcher4Kodi; Filename: {app}\Launcher4Kodi.exe ;HotKey: "ctrl+alt+F10"
Name: {group}\Launcher4KodiGUI; Filename: {app}\Launcher4KodiGUI.exe
Name: {commondesktop}\Launcher4Kodi GUI; Filename: {app}\Launcher4KodiGUI.exe; Tasks: desktopicon; Comment: "GUI for Launcher4Kodi Settings"
Name: {group}\Shutdown; Filename: {app}\Windows8-HybridShutdown.exe ; IconFilename: {app}\Windows8-HybridShutdown.ico ; Components: OptionMetroStart
Name: {group}\Restart; Filename: {app}\Windows8-Restart.exe ; IconFilename: {app}\Windows8-Restart.ico ;  Components: OptionMetroStart
Name: {group}\Sleep; Filename: {app}\Windows8-Sleep.exe ; IconFilename: {app}\Windows8-Sleep.ico ; Components: OptionMetroStart
Name: {commondesktop}\Shutdown; Filename: {app}\Windows8-HybridShutdown.exe ; IconFilename: {app}\Windows8-HybridShutdown.ico ; Components: OptionDesktopPowerButtons
Name: {commondesktop}\Restart; Filename: {app}\Windows8-Restart.exe ; IconFilename: {app}\Windows8-Restart.ico ;  Components: OptionDesktopPowerButtons
Name: {commondesktop}\Sleep; Filename: {app}\Windows8-Sleep.exe ; IconFilename: {app}\Windows8-Sleep.ico ; Components: OptionDesktopPowerButtons
Name: {group}\{cm:UninstallProgram,Launcher4Kodi}; Filename: {uninstallexe}

[Tasks]
Name: desktopicon; Description: Add Launcher4KodiGUI shortcut to Desktop; GroupDescription: {cm:AdditionalIcons}
Name: pintotaskbar; Description: Add Launcher4KodiGUI shortcut to Taskbar [Pin to Taskbar] ; GroupDescription: {cm:AdditionalIcons}; MinVersion: 6.1

[Run]

Filename: {win}\regedit.exe; Description: Install Custom MCE Registry; Parameters: /s {userappdata}\Launcher4Kodi\CustomMCERegistry.reg; Flags: runhidden nowait; Components:Option\CustomMCE 
Filename: {win}\regedit.exe; Description: Install Original MCE Registry; Parameters: /s {userappdata}\Launcher4Kodi\OriginalMCERegistry.reg; Flags: runhidden nowait; Components:Option\OriginalMCE 
Filename: {app}\PinToTaskbar.exe; Tasks: Pintotaskbar
Filename: {app}\Launcher4KodiGUI.exe; Description: Run Launcher4KodiGUI; Flags: nowait postinstall skipifsilent; WorkingDir: {app}  
Filename: {app}\Launcher4Kodi.exe; Description: Run Launcher4Kodi; Flags: nowait postinstall; WorkingDir: {app}

[UninstallDelete]
Name: {userappdata}\Launcher4Kodi; Type: dirifempty
Name: {app};  Type: dirifempty